#!/usr/bin/perl
use strict;
use warnings;

my $w = 35;  # width of the hexagon side
my $s = 5;  # space between hexagons
my $f = 16;  # font size of letters
my $mobile = shift;
my $fmt = "%.4f";

my $w2 = $w/2;
my $s2 = $s/2;

my $w1 = sqrt($w**2 - $w2**2);
my $s1 = sqrt($s**2 - $s2**2);

my $svgx = 4*$w + 2*$s1 + 2*$w2;
my $svgy = 6*$w1 + 2*$s;

my $OX = $svgx/2;
my $OY = $svgy/2;

my $svg;

sub do_hex {
    my ($X, $Y, $C, $color) = @_;
    
    my $Ox = $OX + $X;
    my $Oy = $OY + $Y;

    my $fx = sprintf($fmt, $Ox - $f*0.50);
    my $fy = sprintf($fmt, $Oy + $f*0.45);

    my @hex = (
        [ $Ox-$w,  $Oy ],
        [ $Ox-$w2, $Oy-$w1 ],
        [ $Ox+$w2, $Oy-$w1 ],
        [ $Ox+$w,  $Oy ],
        [ $Ox+$w2, $Oy+$w1 ],
        [ $Ox-$w2, $Oy+$w1 ],
    );

    my $points = '';
    for my $h (@hex) {
        $points .= sprintf("$fmt,$fmt,", $h->[0], $h->[1]);
    }
    $points =~ s{,\z}{}xms;
    my $onclick = $mobile? qq!onclick="add_let('$C')"\n    !: '';
    my $cursor  = qq!style="cursor: !
                . ($mobile? 'pointer': 'default')
                . qq!"\n    !
                ;
    print {$svg} <<"EOH";
<polygon
    ${onclick}points="$points"
    fill="$color"
    $cursor></polygon>
<text
    x="$fx"
    y="$fy"
    fill="black"
    ${cursor}${onclick}class=lets
>$C</text>
EOH
}

sub init {
    open $svg, '>', 'svg.html';
    print {$svg} <<"EOH";
<html>
<head>
<style>
body {
    margin-top: .5in;
    margin-left: .5in;
}
.lets {
    font-family: Arial;
    font-size: ${f}pt;
    font-weight: bold;
    color: black;
}
</style>
<script>
var lets;
function init() {
    lets = document.getElementById('lets');
}
function add_let(c) {
    lets.textContent += c; 
}
</script>
</head>
<body>
<svg
width="$svgx"
height="$svgy"
>
EOH
}

sub finis {
    print {$svg} <<"EOH";
</svg>
<p>
<span class=lets id=lets></span>
</body>
</html>
<script>init();</script>
EOH
    close $svg;
}

my $gray  = '#e0e0e0';
my $red   = '#ff0000';
my $green = '#22ff22';

init();
do_hex(              0,             0, 'LET0', 'gold');
do_hex(              0, -1*(2*$w1+$s), 'LET1', $gray);
do_hex(              0,  1*(2*$w1+$s), 'LET2', $gray);
do_hex( 1*($w+$w2+$s1),   1*($w1+$s2), 'LET3', $gray);
do_hex(-1*($w+$w2+$s1),  -1*($w1+$s2), 'LET4', $gray);
do_hex(-1*($w+$w2+$s1),   1*($w1+$s2), 'LET5', $gray);
do_hex( 1*($w+$w2+$s1),  -1*($w1+$s2), 'LET6', $gray);
finis();
