#!/usr/bin/perl
use strict;
use warnings;

use CGI;
my $q = CGI->new();
print $q->header();
my %param = $q->Vars();

use BeeUtil qw/
    display_clues
/;

use Date::Simple qw/
    date
/;

my $date = $param{date};
my $format = $param{format};
my $person_id = $param{person_id};
my %was_found = map { $_ => 1 }
                split ' ', $param{found};
# to be set:
my $show_date;
my $name;
my %clue_for;

use Bee_DBH qw/
    $dbh
/;

if (my ($ncp) = $date =~ m{\A CP(\d+) \z}xms) {
    $show_date = $date;
    my $href = do "community_puzzles/$ncp.txt";
    $name = $href->{name};
    %clue_for = $href->{clues} =~ m{ ([a-z]+) [\^] ([^~]*) [~] }xmsg;
}
else {
    $show_date = date($date)->format("%B %e, %Y");
    my $sth = $dbh->prepare(<<'EOS');

    SELECT name
      FROM bee_person
     WHERE id = ?

EOS
    $sth->execute($person_id);
    my ($name) = $sth->fetchrow_array();

    my $sth_clues = $dbh->prepare(<<'EOS');

    SELECT word, clue
      FROM bee_clue
     WHERE date = ?
       AND person_id = ?

EOS
    $sth_clues->execute($date, $person_id);
    while (my ($word, $clue) = $sth_clues->fetchrow_array()) {
        $clue_for{$word} = $clue;
    }
}
display_clues(first          => 0,
              format         => $format,
              show_date      => $show_date,
              date           => $date,
              person_id      => $person_id, 
              name           => $name,
              clue_for_href  => \%clue_for,
              was_found_href => \%was_found,
);
